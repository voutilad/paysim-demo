plugins {
    id 'java'
    id 'application'
}

group 'io.sisu'
version '0.8.1'
description 'Using PaySim, populates a graph in a target Neo4j graph database or to CSV files'

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    implementation 'com.github.voutilad:paysim:2.7.0'
    implementation 'org.neo4j.driver:neo4j-java-driver:4.3.2'
    implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'
    implementation 'org.slf4j:slf4j-simple:1.7.30'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

application {
    mainClass.set("io.sisu.paysim.App")
}

task packagePaySimProperties(type: Copy) {
    from '.'
    include 'PaySim.properties'
    include 'paramFiles/**'
    into "$buildDir/paysimprops"
}
distributions {
    main {
        contents {
            from "$buildDir/paysimprops"
        }
    }
}

distZip.dependsOn(packagePaySimProperties)
distTar.dependsOn(packagePaySimProperties)

test {
    useJUnitPlatform()
}

task runCsv(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("io.sisu.paysim.App")
    args = ["csv"]
}

task runBolt(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("io.sisu.paysim.App")
    args = ["bolt"]
}
